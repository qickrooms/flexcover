<?xml version="1.0" encoding="utf-8"?>
<!-- 
 * Copyright (c) 2008 Allurent, Inc.
 *
 * Permission is hereby granted, free of charge, to any person
 * obtaining a copy of this software and associated documentation
 * files (the "Software"), to deal in the Software without restriction,
 * including without limitation the rights to use, copy, modify,
 * merge, publish, distribute, sublicense, and/or sell copies of the
 * Software, and to permit persons to whom the Software is furnished
 * to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be
 * included in all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
 * EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
 * OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
 * IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
 * CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
 * TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE
 * OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 -->
<mx:HBox 
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	show="showSourceView()" 
	hide="hideSourceView()" xmlns:ac="com.adobe.ac.*">
	
	<mx:Script>
		<![CDATA[
			import com.allurent.coverage.model.application.ProjectModel;			
			import com.allurent.coverage.model.FunctionModel;
			import com.allurent.coverage.model.ClassModel;
			import mx.events.PropertyChangeEvent;
            
			[Bindable]
			public var project:ProjectModel;
			[Bindable]
			public var classModel:ClassModel;
			[Bindable]
			public var lineNum:uint = 0;
			[Bindable]
			public var hasInvalidated:Boolean;			         
	            
            public function hideSourceView():void
            {
            	removeAllChildren();
            }
               
            public function showSourceView():void
            {
                var sourceView:SourceCoverageView = null;
                if (numChildren > 0)
                {
                    sourceView = getChildAt(0) as SourceCoverageView;
                }
                if (sourceView == null || sourceView.classModel != classModel)
                {
                    removeAllChildren();
                    
                    sourceView = new SourceCoverageView();
                    sourceView.percentWidth = sourceView.percentHeight = 100;
                    addChild(sourceView);
                    
                    var gapFinder:GapFinderView = new GapFinderView();
                    gapFinder.width = 10;
                    gapFinder.percentHeight = 100;
                    addChild(gapFinder);
                    
                    var gapBinding:Function = function(e:PropertyChangeEvent):void {
                        if (e.property == "gapOffsets")
                        {
                            gapFinder.gapOffsets = e.newValue as Array;
                        }
                    }
                    sourceView.addEventListener(PropertyChangeEvent.PROPERTY_CHANGE, gapBinding);
                    
                    sourceView.show(project, classModel);
                }
                sourceView.scrollToLine(lineNum);
            }
		]]>
	</mx:Script>
	
	<ac:ObserveValue 
		source="{hasInvalidated}" 
		handler="showSourceView" 
		value="{true}"/>	
	
</mx:HBox>